#
# Cmake configuration file
#

############################# INITIAL SECTION ##########################
CMAKE_MINIMUM_REQUIRED(VERSION 3.1 FATAL_ERROR)
# Silently strip whitespace from target_link_libraries
CMAKE_POLICY(SET CMP0004 OLD)


PROJECT(ssdpd C)

SET(PACKAGE_VERSION_MAJOR	1)
SET(PACKAGE_VERSION_MINOR	07)
SET(PACKAGE_VERSION_PATCH	0)

SET(PACKAGE_NAME		"${PROJECT_NAME}")
SET(PACKAGE_VERSION		"${PACKAGE_VERSION_MAJOR}.${PACKAGE_VERSION_MINOR}.${PACKAGE_VERSION_PATCH}")
SET(PACKAGE_URL			"https://github.com/rozhuk-im/ssdpd")
SET(PACKAGE_BUGREPORT		"https://github.com/rozhuk-im/ssdpd")
SET(PACKAGE_STRING 		"${PACKAGE_NAME} ${PACKAGE_VERSION}")
SET(PACKAGE_DESCRIPTION		"SSDP announce daemon for UPnP 1.1")
SET(PACKAGE_TARNAME		"${PACKAGE_NAME}-${PACKAGE_VERSION}")

############################# OPTIONS SECTION ##########################

OPTION(ENABLE_OPTIMIZATION	"Enable extra optimizations [default: OFF]"		OFF)
OPTION(ENABLE_COVERAGE		"Build with code coverage options [default: OFF]"	OFF)
OPTION(ENABLE_FULL_DEBUG	"Build with all possible debug [default: OFF]"		OFF)
OPTION(INSTALL_PHP_MEDIA_SERVER	"Install PHP UPnP/DLNA Media server [default: ON]"	OFF)

# Now CMAKE_INSTALL_PREFIX is a base prefix for everything.
IF(NOT CONFDIR)
	SET(CONFDIR "${CMAKE_INSTALL_PREFIX}/etc/ssdpd")
ENDIF()

IF(NOT RUNDIR)
	SET(RUNDIR "/var/run")
ENDIF()

IF(NOT WWWDIR)
	SET(WWWDIR "${CMAKE_INSTALL_PREFIX}/share/ssdpd/www")
ENDIF()


############################# INCLUDE SECTION ##########################

INCLUDE(CheckIncludeFiles)
INCLUDE(CheckFunctionExists)
INCLUDE(CheckSymbolExists)
INCLUDE(CheckCSourceCompiles)
INCLUDE(CheckCSourceRuns)
INCLUDE(CheckLibraryExists)
INCLUDE(CheckCCompilerFlag)
INCLUDE(CMakeParseArguments)

FIND_PACKAGE(PkgConfig REQUIRED)
FIND_LIBRARY(PTHREAD_LIBRARY pthread)
LIST(APPEND CMAKE_REQUIRED_LIBRARIES ${PTHREAD_LIBRARY})

############################# MACRO SECTION ############################
FUNCTION(INSTALL_IF_NOT_EXISTS src dest destname suffix)
	IF(NOT IS_ABSOLUTE "${src}")
		SET(src "${CMAKE_CURRENT_SOURCE_DIR}/${src}")
	ENDIF()
	IF(NOT IS_ABSOLUTE "${dest}")
		SET(dest "${CMAKE_INSTALL_PREFIX}/${dest}")
	ENDIF()
	GET_FILENAME_COMPONENT(src_name "${src}" NAME)
	GET_FILENAME_COMPONENT(dest_name "${destname}" NAME)
	IF(NOT EXISTS "${dest}/${dest_name}${suffix}")
		SET(src_tmp "${CMAKE_CURRENT_BINARY_DIR}/${dest_name}${suffix}")
		CONFIGURE_FILE(${src} ${src_tmp} @ONLY)
		INSTALL(FILES ${src_tmp} DESTINATION ${dest})
	ENDIF()
ENDFUNCTION()

FUNCTION(INSTALL_SCRIPT src dest)
	IF(NOT IS_ABSOLUTE "${src}")
		SET(src "${CMAKE_CURRENT_SOURCE_DIR}/${src}")
	ENDIF()
	IF(NOT IS_ABSOLUTE "${dest}")
		SET(dest "${CMAKE_INSTALL_PREFIX}/${dest}")
	ENDIF()
	GET_FILENAME_COMPONENT(src_name "${src}" NAME)
	SET(src_tmp "${CMAKE_CURRENT_BINARY_DIR}/${src_name}")
	CONFIGURE_FILE(${src} ${src_tmp} @ONLY)
	INSTALL(PROGRAMS ${src_tmp} DESTINATION ${dest})
ENDFUNCTION()

FUNCTION(INSTALL_CONFIG src dest)
	IF(NOT IS_ABSOLUTE "${src}")
		SET(src "${CMAKE_CURRENT_SOURCE_DIR}/${src}")
	ENDIF()
	IF(NOT IS_ABSOLUTE "${dest}")
		SET(dest "${CMAKE_INSTALL_PREFIX}/${dest}")
	ENDIF()
	GET_FILENAME_COMPONENT(src_name "${src}" NAME)
	SET(src_tmp "${CMAKE_CURRENT_BINARY_DIR}/${src_name}")
	CONFIGURE_FILE(${src} ${src_tmp} @ONLY)
	INSTALL(FILES ${src_tmp} DESTINATION ${dest})
ENDFUNCTION()

############################# CONFIG SECTION ###########################

# Prefer local include dirs to system ones.
INCLUDE_DIRECTORIES("${CMAKE_CURRENT_SOURCE_DIR}/"
		"${CMAKE_CURRENT_SOURCE_DIR}/src"
		"${CMAKE_CURRENT_SOURCE_DIR}/src/liblcb/include"
		"${CMAKE_BINARY_DIR}/src")

SET(TAR "tar")

# Platform specific configuration.
IF(CMAKE_SYSTEM_NAME MATCHES "^.*BSD$|DragonFly")
	ADD_DEFINITIONS(-DFREEBSD -D_BSD_SOURCE)
	MESSAGE(STATUS "Configuring for BSD system")
	SET(TAR "gtar")
ENDIF()

IF(CMAKE_SYSTEM_NAME STREQUAL "Darwin")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_BSD_SOURCE -DDARWIN")
	MESSAGE(STATUS "Configuring for Darwin")
	SET(TAR "gnutar")
	SET(CMAKE_FIND_FRAMEWORK "NEVER")
ENDIF()


IF(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_GNU_SOURCE -D__USE_GNU=1")
	ADD_DEFINITIONS(-D_GNU_SOURCE -DLINUX)
	ADD_DEFINITIONS(-D__USE_GNU=1)
	IF(BUILD_CPU_MODE STREQUAL "32")
		ADD_DEFINITIONS(-D_FILE_OFFSET_BITS=64 -D_LARGEFILE64_SOURCE)
	ENDIF()

	LIST(APPEND CMAKE_REQUIRED_LIBRARIES rt)
ENDIF()


# Process with warn flags.
SET(CMAKE_C_WARN_FLAGS "")
IF(NOT CMAKE_C_OPT_FLAGS AND ENABLE_FULL_DEBUG)
	CHECK_C_COMPILER_FLAG(-W			SUPPORT_W)
	CHECK_C_COMPILER_FLAG(-Wall			SUPPORT_WALL)
	CHECK_C_COMPILER_FLAG(-Wpointer-arith		SUPPORT_WPOINTER)
	CHECK_C_COMPILER_FLAG(-Wno-unused-parameter	SUPPORT_WPARAM)
	CHECK_C_COMPILER_FLAG(-Wno-unused-function	SUPPORT_WFUNCTION)
	CHECK_C_COMPILER_FLAG(-Wunused-variable		SUPPORT_WUNUSED_VAR)
	CHECK_C_COMPILER_FLAG(-Wno-pointer-sign		SUPPORT_WPOINTER_SIGN)
	CHECK_C_COMPILER_FLAG(-Wno-sign-compare		SUPPORT_WSIGN_COMPARE)
	CHECK_C_COMPILER_FLAG(-Wstrict-prototypes	SUPPORT_WSTRICT_PROTOTYPES)
	CHECK_C_COMPILER_FLAG(-pedantic			SUPPORT_PEDANTIC_FLAG)
	CHECK_C_COMPILER_FLAG(-Wno-unused-const-variable SUPPORT_WNO_UNUSED_CONST)
	# GCC 6 specific.
	CHECK_C_COMPILER_FLAG(-Wnull-dereference	SUPPORT_WNULL_DEREFERENCE)
	CHECK_C_COMPILER_FLAG(-Wduplicated-cond		SUPPORT_WDUPLICATED_COND)
	# GCC 7 specific.
	CHECK_C_COMPILER_FLAG(-Wimplicit-fallthrough	SUPPORT_WIMPLICIT_FALLTHROUGH)

	IF(SUPPORT_W)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -W")
	ENDIF()
	IF(SUPPORT_WALL)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wall")
	ENDIF()
	IF(SUPPORT_WPOINTER)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wpointer-arith")
	ENDIF()
	IF(SUPPORT_WPARAM)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wno-unused-parameter")
	ENDIF()
	IF(SUPPORT_WFUNCTION)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wno-unused-function")
	ENDIF()
	IF(SUPPORT_WUNUSED_VAR)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wunused-variable")
	ENDIF()
	IF(SUPPORT_WPOINTER_SIGN)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wno-pointer-sign")
	ENDIF()
	IF(SUPPORT_WSTRICT_PROTOTYPES)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wstrict-prototypes")
	ENDIF()
	IF(SUPPORT_PEDANTIC_FLAG)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -pedantic")
	ENDIF()
	IF(SUPPORT_WNULL_DEREFERENCE)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wnull-dereference")
	ENDIF()
	IF(SUPPORT_WDUPLICATED_COND)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wduplicated-cond")
	ENDIF()
	IF(SUPPORT_WLOGICAL_OP)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wlogical-op")
	ENDIF()
	IF(SUPPORT_WNO_UNUSED_CONST)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wno-unused-const-variable")
	ENDIF()
	IF(SUPPORT_WSIGN_COMPARE)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wno-sign-compare")
	ENDIF()
	IF(SUPPORT_WIMPLICIT_FALLTHROUGH)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -Wno-implicit-fallthrough")
	ENDIF()
ENDIF()

IF(NOT "${CMAKE_C_COMPILER_ID}" MATCHES SunPro)
	CHECK_C_COMPILER_FLAG("-std=c11"	SUPPORT_STD11_FLAG)
	IF(SUPPORT_STD11_FLAG)
		SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -std=c11")
	ELSE()
		CHECK_C_COMPILER_FLAG("-std=c99"	SUPPORT_STD99_FLAG)
		IF(SUPPORT_STD99_FLAG)
			SET(CMAKE_C_WARN_FLAGS "${CMAKE_C_WARN_FLAGS} -std=c99")
		ENDIF()
	ENDIF()
ENDIF()

CHECK_C_COMPILER_FLAG(-fPIC			SUPPORT_FPIC)
IF(SUPPORT_FPIC)
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -fPIC")
ENDIF()

# Optimization flags
IF(NOT CMAKE_C_OPT_FLAGS)
	IF(ENABLE_OPTIMIZATION)
		CHECK_C_COMPILER_FLAG(-flto SUPPORT_FLTO)
		IF(SUPPORT_FLTO)
			IF(NOT CMAKE_GCC_AR OR NOT CMAKE_GCC_RANLIB)
				FIND_PROGRAM(CMAKE_GCC_AR NAMES "gcc-ar")
				FIND_PROGRAM(CMAKE_GCC_RANLIB NAMES "gcc-ranlib")
				MARK_AS_ADVANCED(CMAKE_GCC_AR CMAKE_GCC_RANLIB)
			ENDIF()
			IF(CMAKE_GCC_AR AND CMAKE_GCC_RANLIB)
				SET(CMAKE_C_OPT_FLAGS "-g -O3 -fstrict-aliasing -flto")
				SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -flto")
				SET(CMAKE_C_ARCHIVE_CREATE "${CMAKE_GCC_AR} cr <TARGET> <LINK_FLAGS> <OBJECTS>")
				SET(CMAKE_C_ARCHIVE_APPEND "${CMAKE_GCC_AR} r <TARGET> <LINK_FLAGS> <OBJECTS>")
				SET(CMAKE_C_ARCHIVE_FINISH "${CMAKE_GCC_RANLIB} <TARGET>")
				SET(CMAKE_CXX_ARCHIVE_CREATE "${CMAKE_GCC_AR} cr <TARGET> <LINK_FLAGS> <OBJECTS>")
				SET(CMAKE_CXX_ARCHIVE_APPEND "${CMAKE_GCC_AR} r <TARGET> <LINK_FLAGS> <OBJECTS>")
				SET(CMAKE_CXX_ARCHIVE_FINISH "${CMAKE_GCC_RANLIB} <TARGET>")
			ENDIF()
		ELSE()
			SET(CMAKE_C_OPT_FLAGS "-g -O3 -fstrict-aliasing")
		ENDIF()
	ELSE()
		IF(ENABLE_FULL_DEBUG)
			SET(CMAKE_C_OPT_FLAGS "-g -O0 -fstrict-aliasing")
		ELSE()
			SET(CMAKE_C_OPT_FLAGS "-g -O2 -fstrict-aliasing")
		ENDIF()
	ENDIF()
ENDIF()

IF(ENABLE_COVERAGE)
	SET(CMAKE_C_OPT_FLAGS "-g -O0 -fno-strict-aliasing")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} --coverage")
	SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} --coverage")
ENDIF()

SET(CMAKE_C_FLAGS "${CMAKE_C_OPT_FLAGS} ${CMAKE_C_FLAGS} ${CMAKE_C_WARN_FLAGS}")

# Check platform specific includes.
CHECK_INCLUDE_FILES(sys/types.h			HAVE_SYS_TYPES_H)
CHECK_INCLUDE_FILES(sys/time.h			HAVE_SYS_TIME_H)

# Check platform API.
CHECK_FUNCTION_EXISTS(memset_s			HAVE_MEMSET_S)
CHECK_FUNCTION_EXISTS(explicit_bzero		HAVE_EXPLICIT_BZERO)
CHECK_FUNCTION_EXISTS(memrchr			HAVE_MEMRCHR)
CHECK_FUNCTION_EXISTS(memmem			HAVE_MEMMEM)
CHECK_FUNCTION_EXISTS(strncasecmp		HAVE_STRNCASECMP)
CHECK_FUNCTION_EXISTS(reallocarray		HAVE_REALLOCARRAY)
CHECK_FUNCTION_EXISTS(pipe2			HAVE_PIPE2)
CHECK_FUNCTION_EXISTS(accept4			HAVE_ACCEPT4)

# Check macros.
CHECK_SYMBOL_EXISTS(PATH_MAX	limits.h	HAVE_PATH_MAX)
CHECK_SYMBOL_EXISTS(MAXPATHLEN	sys/param.h	HAVE_MAXPATHLEN)
CHECK_SYMBOL_EXISTS(SOCK_NONBLOCK sys/socket.h	HAVE_SOCK_NONBLOCK)

CONFIGURE_FILE(config.h.cmake src/config.h)
ADD_DEFINITIONS(-DHAVE_CONFIG_H)

################################ SUBDIRS SECTION #######################

ADD_SUBDIRECTORY(src)

############################ TARGETS SECTION ###########################

ADD_CUSTOM_TARGET(dist ${CMAKE_CURRENT_SOURCE_DIR}/dist.sh
	"${CMAKE_BINARY_DIR}/${PACKAGE_TARNAME}.tar.xz" "${TAR}"
	COMMENT "Create source distribution"
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})

##################### INSTALLATION #####################################

# Configs
INSTALL(CODE "FILE(MAKE_DIRECTORY ${CONFDIR})")

INSTALL_IF_NOT_EXISTS(ssdpd.conf ${CONFDIR} "ssdpd.conf" ".sample")

IF(CMAKE_SYSTEM_NAME MATCHES "^.*BSD$|DragonFly")
	INSTALL_SCRIPT(freebsd/ssdpd "${CMAKE_INSTALL_PREFIX}/etc/rc.d/")
ENDIF()

# Install PHP UPnP/DLNA Media server
IF(INSTALL_PHP_MEDIA_SERVER)
	INSTALL(DIRECTORY "www/upnp" DESTINATION ${WWWDIR})
	INSTALL(DIRECTORY DESTINATION "${WWWDIR}/upnpdata")
	INSTALL_CONFIG("nginx/nginx-upnp-full.conf" ${CONFDIR})
	INSTALL_CONFIG("nginx/nginx-upnp-server.conf" ${CONFDIR})
	INSTALL_CONFIG("nginx/nginx-upnp-handler.conf" ${CONFDIR})
	INSTALL_CONFIG("php/upnp-server.conf" "${CMAKE_INSTALL_PREFIX}/etc/php-fpm.d/")
ENDIF()

